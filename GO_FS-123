#!/bin/tcsh -ef
# add 'x' to '-ef' to trace code as it is executed

##### GO_FS1 - The "Easy" button #####
##### Usage: GO_FS-123 <subjects directory name>
##### Feeds multiple subjects into autorecon1, autorecon2, and autorecon3
##### Requires that original scan data be placed in $SUBJECTS_DIR/_orig_
##### 	(Place each subject's anat series in separate folders)



echo "\n <><><><><><><><><><><>< GO FreeSurfer! ><><><><><><><><><><><> \n"



### Check if subjects directory specified ###
if ( "$1" != "" ) then
  set subjdir = $1
else
  echo "\n ERROR: No subjects directory specified \n"
  exit 1
endif

### Set FREESURFER_HOME and SUBJECTS_DIR ###

setenv FREESURFER_HOME /home/omid/freesurfer

if ( -e $FREESURFER_HOME/subjects/$subjdir) then
  setenv SUBJECTS_DIR $FREESURFER_HOME/subjects/$subjdir
else
  echo "\n ERROR: Specified subjects directory does not exist \n"
  exit 1
endif

### Check if original scan data exists ###

if ( ! -e $SUBJECTS_DIR/_orig_ ) then
  echo "\n ERROR: Original scan data not found. \n"
  exit 1
endif

### disable functional tools ###

setenv NO_FSFAST

##### (To enable functional tools, disable the above line, enable the following:)
##### (HAVE NOT TESTED THIS YET) (MAYBE BETTER IF NO_FSFAST ANYWAYS)
#setenv FUNCTIONALS_DIR $FREESURFER_HOME/sessions
#	echo "FUNCTIONALS_DIR = $FUNCTIONALS_DIR"
##### (and add the following to ~/matlab/startup.m)
#fsfasthome = getenv('FSFAST_HOME');
#fsfasttoolbox = sprintf('%s/toolbox',fsfasthome');
#path(path,fsfasttoolbox);

### Set up FreeSurfer ###

setenv prompt
source $FREESURFER_HOME/FreeSurferEnv.csh
echo ""











### List out directories in _orig_ , set subj array, prompt to continue ###

echo "Found the following subjects: \n"

set i = 0
set subjID

foreach d ($SUBJECTS_DIR/_orig_/*)
  @ i++
  set subjID = ($subjID $d:t)
  echo "   Subject "$i": " $subjID[$i]
end
set subjCount = $i

set response
while ($response != "n" && $response != "y")
  echo -n "\n Process these $subjCount subjects? [y/n] > "
  set response = $<
end
if ($response == "n") then
  echo "\n Program ended by user \n"
  exit 1
endif




### Prompt user for First Anat Filename's ### 

set firstAnatSame
while ($firstAnatSame != "y" && $firstAnatSame != "n")
  echo -n "\n Are all the anat series named identically? [y/n]: > "
  set firstAnatSame = $<
end
switch ($firstAnatSame)
  case [y]:
    echo -n "\n Please enter the first filename (e.g. p1_000.dcm) for all anat series: > "
    set firstAnat = $<
    breaksw
  case [n]:
    echo "\n Please enter the first filename (e.g. p1_000.dcm) in the anat series for:"
    set i = 0
    set firstAnat
    while ($i < $subjCount)
      @ i++
      echo -n "    "$subjID[$i]": > "
      set firstAnat = ($firstAnat $<)
    end	
    breaksw	
endsw



### Run autorecon1 for each subject ###

cd $SUBJECTS_DIR

switch ($firstAnatSame)
  case [y]:
    set i = 0
    while ($i < $subjCount)
      @ i++
    echo "\n<><><><><><><><><><><><| NOW PROCESSING <"$subjID[$i]"> |><><><><><><><><><><><>\n"
      recon-all -i $SUBJECTS_DIR/_orig_/$subjID[$i]/$firstAnat -autorecon1 -s $subjID[$i]
      recon-all -autorecon2 -s $subjID[$i]
      recon-all -autorecon3 -s $subjID[$i]
    end
    breaksw
  case [n]:
    set i = 0
    while ($i < $subjCount)
      @ i++
    echo "\n<><><><><><><><><><><><| NOW PROCESSING <"$subjID[$i]"> |><><><><><><><><><><><>\n"
      recon-all -i $SUBJECTS_DIR/_orig_/$subjID[$i]/$firstAnat[$i] -autorecon1 -s $subjID[$i]
      recon-all -autorecon2 -s $subjID[$i]
      recon-all -autorecon3 -s $subjID[$i]
    end
    breaksw
endsw





echo ""
date +"Finished $0 without error at %T on %a %b %d %Y"
echo ""
exit 0











#######################
### Simple working loop
#set i = 0
#while ($i < 10)
#	echo $i
#	@ i++
#end
#######################




#############
### TO DO ###
#############
# why don't env's remain 'set' after running this? (they do after running SetUpFreeSurfer.csh or FreeSurferEnv.csh)
# why can't FS_FAST be set up?
